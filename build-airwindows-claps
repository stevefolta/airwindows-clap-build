#!/usr/bin/env sqs

airwindows-sources = nil

fn main
	read-settings("~/.config/build-airwindows-claps/settings")
	read-settings("settings")
	setup
	if !airwindows-sources
		fail('Configure "airwindows-sources" in your "settings" file.')
	add-plugin("Capacitor2")
	add-plugin("Reverb")
	build-all


plugins = []

string-setting-re = Regex(r'[[:space:]]*(?P<name>[[:alnum:]-]+)[[:space:]]*=[[:space:]]*"(?P<value>[^"]*)"')

fn read-settings(path)
	path = Path(path)
	if !path.is-file
		return
	with file = File(path)
		for line: file.lines
			match = string-setting-re.match(line)
			if !match
				continue
			if match["name"] == "airwindows-sources"
				airwindows-sources = match["value"]

cxx = nil
c-flags = [ "-I." "-fPIC" "-Wno-multichar" "-Wfatal-errors" ]

fn setup
	$ mkdir -p sources build
	cxx = env["CXX"] || "c++"

create-effect-instance-re = Regex(r'createEffectInstance\(')

cpp-template = r'/* NAME_CLAP.cpp */

#include "SRC_DIR/NAME.h"

AudioEffectX* create_NAME(audioMasterCallback callback) { return new NAME(callback); }
'

fn add-plugin(name)
	src-dir = Path("{airwindows-sources}/plugins/LinuxVST/src/{name}").string
	if !Path(src-dir).is-dir
		fail("Couldn't find source: {name}")

	# Copy Plugin.cpp, with changes.
	with in-file = File("{src-dir}/{name}.cpp")
		with out-file = File("sources/{name}.cpp", "w")
			for line: in-file.lines
				if create-effect-instance-re.match(line)
					continue
				out-file.write(line)
				out-file.write("\n")

	# Make Plugin_CLAP.cpp.
	with file = File("sources/{name}_CLAP.cpp", "w")
		file.write(cpp-template.replace("SRC_DIR", src-dir).replace("NAME", name))

	plugins.append(name)

fn build-all
	src-dir = Path("{airwindows-sources}/plugins/LinuxVST/src").string
	object-files = []
	for name: plugins
		# Plugin.cpp.
		print("Compiling {name}.cpp...")
		object-file = "build/{name}.o"
		$ {cxx} -c {c-flags} "-I{src-dir}/{name}" "sources/{name}.cpp" -o {object-file} || {fail()}
		object-files.append(object-file)
		# PluginProc.cpp.
		print("Compiling {name}Proc.cpp...")
		object-file = "build/{name}Proc.o"
		$ {cxx} -c {c-flags} "-I{src-dir}/{name}" "{src-dir}/{name}/{name}Proc.cpp" -o {object-file} || {fail()}
		object-files.append(object-file)
		# Plugin_CLAP.cpp.
		print("Compiling {name}_CLAP.cpp...")
		object-file = "build/{name}_CLAP.o"
		$ {cxx} -c {c-flags} "sources/{name}_CLAP.cpp" -o {object-file} || {fail()}
		object-files.append(object-file)

	for name: [ "AudioEffectX" "AirwindowsCLAP" ]
		print("Compiling {name}.cpp...")
		object-file = "build/{name}.o"
		$ {cxx} -c "{name}.cpp" {c-flags} -o {object-file} || {fail()}
		object-files.append(object-file)

	print("Linking...")
	$ {cxx} {object-files} -shared -o "Airwindows.clap"


main()


