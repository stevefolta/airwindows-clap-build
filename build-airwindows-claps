#!/usr/bin/env sqs

airwindows-sources = nil

fn main
	read-settings("~/.config/build-airwindows-claps/settings")
	read-settings("settings")
	setup
	if !airwindows-sources
		fail('Configure "airwindows-sources" in your "settings" file.')
	add-plugin("Capacitor2")
	add-plugin("Reverb")
	build-all


plugins = []

string-setting-re = Regex(r'[[:space:]]*(?P<name>[[:alnum:]-]+)[[:space:]]*=[[:space:]]*"(?P<value>[^"]*)"')

fn read-settings(path)
	path = Path(path)
	if !path.is-file
		return
	with file = File(path)
		for line: file.lines
			match = string-setting-re.match(line)
			if !match
				continue
			if match["name"] == "airwindows-sources"
				airwindows-sources = match["value"]

cxx = nil
c-flags = [ "-I." "-fPIC" "-Wno-multichar" "-Wfatal-errors" ]

fn setup
	$ mkdir -p sources build
	cxx = env["CXX"] || "c++"

create-effect-instance-re = Regex(r'createEffectInstance\(')

cpp-template = r'/* NAME_CLAP.cpp */

#include "SRC_DIR/NAME.h"

AudioEffectX* create_NAME() { return new NAME(nullptr); }
'

fn add-plugin(name)
	src-dir = Path("{airwindows-sources}/plugins/LinuxVST/src/{name}").string
	if !Path(src-dir).is-dir
		fail("Couldn't find source: {name}")

	# Copy Plugin.cpp, with changes.
	with in-file = File("{src-dir}/{name}.cpp")
		with out-file = File("sources/{name}.cpp", "w")
			for line: in-file.lines
				if create-effect-instance-re.match(line)
					continue
				out-file.write(line)
				out-file.write("\n")

	# Make Plugin_CLAP.cpp.
	with file = File("sources/{name}_CLAP.cpp", "w")
		file.write(cpp-template.replace("SRC_DIR", src-dir).replace("NAME", name))

	plugins.append(name)

file-ext-re = Regex(r'^(?P<root>.*)\.(?P<ext>[[:alnum:]]+)$')

fn build-all
	object-files = []
	fn compile(c-source, extra-c-flags)
		our-c-flags = c-flags
		if extra-c-flags
			our-c-flags += extra-c-flags
		name = file-ext-re.match(Path(c-source).basename)['root']
		print("Compiling {name}.cpp...")
		object-file = "build/{name}.o"
		$ {cxx} -c {c-source} {our-c-flags} -o {object-file} || {fail()}
		object-files.append(object-file)

	# The framework.
	for name: [ "AudioEffectX" "AirwindowsCLAP" ]
		compile("{name}.cpp")

	# The plugins.
	src-dir = Path("{airwindows-sources}/plugins/LinuxVST/src").string
	for name: plugins
		# Plugin.cpp.
		compile("sources/{name}.cpp", [ "-I{src-dir}/{name}" ])
		# PluginProc.cpp.
		compile("{src-dir}/{name}/{name}Proc.cpp", [ "-I{src-dir}/{name}" ])
		# Plugin_CLAP.cpp.
		compile("sources/{name}_CLAP.cpp")

	# The vector of plugins.
	with file = File("sources/Plugins_vector.cpp", "w")
		file.write('#include "AirwindowsCLAP.h"\n')
		file.write('\n')
		for name: plugins
			file.write('extern AudioEffectX* create_{name}();\n')
		file.write('\n')
		file.write('std::vector<PluginEntry> plugins = {{\n')
		for name: plugins
			file.write('\t{{ .create = create_{name}, }},\n')
		file.write('\t}};\n')
		file.write('\n')
	# Compile it.
	compile("sources/Plugins_vector.cpp")

	print("Linking...")
	$ {cxx} {object-files} -shared -o "Airwindows.clap"


main()


